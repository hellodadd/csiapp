apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_ALIAS_PASSWORD
        }
    }
    compileSdkVersion 23
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.android.csiapp"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1612291900
        versionName "1.1.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "CSIAPP-${variant.buildType.name}-${variant.versionName}.apk".toLowerCase())
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.google.android.gms:play-services-appindexing:8.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile files('libs/BaiduLBS_Android.jar')
    compile files('libs/AMap3DMap_4.1.1_AMapLocation_3.0.0_20161019.jar')
    provided files(getLayoutLibPath())
}

def getLayoutLibPath() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            instr -> properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def compileSdkVersion = android.compileSdkVersion
        Console.println("app compileSdkVersion : " + compileSdkVersion)
        def androidJarPath = sdkDir + "/platforms/" + compileSdkVersion + "/data/layoutlib.jar"
        return androidJarPath
    }
    return rootDir
}
